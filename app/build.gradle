apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "net.kbrz.sample"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "env"

    productFlavors {
        prod {
            dimension "env"
            buildConfigField "String", "BASE_URL", '"https://prod.api.com/"'
        }

        dev {
            dimension "env"
            buildConfigField "String", "BASE_URL", '"https://api.github.com/"'

            versionNameSuffix "-dev"
            applicationIdSuffix '.dev'
        }
    }

}

ext {
    jodaTimeVersion = '2.9.9'

    supportLibVersion = '26.1.0'

    architectureComponentsVersion = '1.0.0-rc1'

    daggerVersion = '2.8'

    rxJavaVersion = '2.1.6'
    rxAndroidVersion = '2.0.1'

    okHttpVersion = '3.9.0'
    retrofitVersion = '2.3.0'
    gsonVersion = '2.8.2'

    paperParcelVersion = '2.0.4'

    glideVersion = '4.3.0'
    glideTransformationsVersion = '3.0.1'

    espressoVersion = '3.0.1'
    jUnitVersion = '4.12'
    mockitoKotlinVersion = '1.5.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:${kotlinVersion}"

    kapt "com.android.databinding:compiler:${gradlePluginVersion}"

    compile "joda-time:joda-time:${jodaTimeVersion}"

    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"

    compile "android.arch.lifecycle:runtime:${architectureComponentsVersion}"
    compile "android.arch.lifecycle:extensions:${architectureComponentsVersion}"
    kapt "android.arch.lifecycle:compiler:${architectureComponentsVersion}"

    compile "com.google.dagger:dagger:${daggerVersion}"
    kapt "com.google.dagger:dagger-compiler:${daggerVersion}"
    kaptTest "com.google.dagger:dagger-compiler:${daggerVersion}"

    compile "io.reactivex.rxjava2:rxjava:${rxJavaVersion}"
    compile "io.reactivex.rxjava2:rxandroid:${rxAndroidVersion}"

    compile "com.google.code.gson:gson:${gsonVersion}"
    compile "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    compile "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    compile "com.squareup.retrofit2:adapter-rxjava2:${retrofitVersion}"

    compile "nz.bradcampbell:paperparcel:${paperParcelVersion}"
    kapt "nz.bradcampbell:paperparcel-compiler:${paperParcelVersion}"

    compile "com.github.bumptech.glide:glide:${glideVersion}"
    compile "jp.wasabeef:glide-transformations:${glideTransformationsVersion}"

    testCompile "junit:junit:${jUnitVersion}"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:${kotlinVersion}"
    testCompile "com.nhaarman:mockito-kotlin-kt1.1:${mockitoKotlinVersion}"

    androidTestCompile("com.android.support.test.espresso:espresso-core:${espressoVersion}", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
